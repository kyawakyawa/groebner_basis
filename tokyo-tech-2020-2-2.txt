(* A(0, 0) *) (* Aは原点に固定する(一般性は失わない)*)
(* B(b1, 0) *) (* 辺ABはx軸上にする(一般性は失わない)*)
(* C(c1, c2)*)
(* M(m1, m2) 外接円の中心 *)
(* P(p1, p2) 外接円上の点 *)
(* r 外接円の半径*)
(* d 答え*)

(* 辺ABと辺ACが同じ長さ *)
f1 = c1 ^2 + c2 ^2 - b1 ^2

(* 辺ABと辺BCが同じ長さ *)
f2 = (c1 - b1)^2 + c2 ^ 2 - b1^2

(* 点Aと点Xの距離はr *)
f3 = m1 ^2 + m2 ^2 - r ^2

(* 点Bと点Xの距離はr *)
f4 = (b1 - m1)^ 2 + (0 - m2)^2 - r ^2

(* 点Cと点Xの距離はr *)
f5 = (c1 - m1)^ 2 + (c2 - m2)^2 - r ^2

(* 点Pと点Xの距離はr *)
f6 = (p1 - m1)^ 2 + (p2 - m2)^2 - r ^2

(* b1 0 は でない *) (* この条件だけで三角形が潰れれることはなくなる *)
f7 = b1 * v - 1

(* AP^2+BP^2+CP^2==d1 (d1が求めたい式に等しい) *)
g = p1 ^2 + p2 ^2 + (p1 - b1)^ 2 + p2 ^ 2 + (p1 - c1)^ 2 + (p2 - c2)^2 - d1


(* AP^4+BP^4+CP^4==d2 (d2が求めたい式に等しい) *)
h = (p1 ^2 + p2 ^2)^2 + ((p1 - b1)^2 + p2 ^2)^ 2 + ((p1 - c1)^2 + (p2 - c2)^ 2)^2 - d2

(* 消去定理により イデアルに含まれる rとd1 or d2のみの変数からなる多項式を探す *)

(* AP^2+BP^2+CP^2 *)
GroebnerBasis[{f1, f2, f3, f4, f5, f6, f7, g}, {b1, c1, c2, m1, m2, p1, p2, v, r, d1}]

(* AP^4+BP^4+CP^4 *)
GroebnerBasis[{f1, f2, f3, f4, f5, f6, f7, h}, {b1, c1, c2, m1, m2, p1, p2, v, r, d2}]